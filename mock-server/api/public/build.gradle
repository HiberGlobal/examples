apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

sourceSets.main.proto.srcDir "../../../hiber-api/"
sourceSets.main.java {
  srcDir "${projectDir}/build/generated/source/proto/main/java"
  srcDir "${projectDir}/build/generated/source/proto/main/grpc"
}
idea.module {
  generatedSourceDirs += files("${projectDir}/build/generated/source/proto/main/java")
  generatedSourceDirs += files("${projectDir}/build/generated/source/proto/main/grpc")
}

dependencies {
  grpc.each { compile it.value }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  dependsOn 'generateProto'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${rootProject.ext.versions.build.protoc}"
  }
  //noinspection GroovyAssignabilityCheck
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.versions.build.grpc}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        // To generate deprecated interfaces and static bindService method,
        // turn the enable_deprecated option to true below:
        option 'enable_deprecated=false'
      }
    }
  }
}
